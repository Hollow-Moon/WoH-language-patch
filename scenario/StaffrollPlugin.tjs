/*----------------------------------------------------------------------------/
/	スタッフロールプラグインVer.1.0 (C) W.Dee		基本機能実装
/							Ver.1.1 (C) TYPE-MOON	拡張・サポート機能追加
/							Ver.1.2 (C) TYPE-MOON	レイヤーではメモリ使用量が大きくなって
/													しまったので、直接baseへ描画することにした。
/							Ver.1.3 (C) TYPE-MOON	縦書き文字に対応。
/----------------------------------------------------------------------------*/
@if(__STAFFROLL_PLUGIN_DEFINED__==0)
@set(__STAFFROLL_PLUGIN_DEFINED__=1)

class TextHolder
{
	var window, parent;
	var l, t, width, height;
	var imageWidth, imageHeight;
	var ml, mt, name, col, opacity, aa;
	var level, scol, sw, sol, sot;
	var vertical;
	var orgX, orgY;
	var magx, magy;
	var font;
	var layer;
	var plugin;

	function TextHolder(window, parent, plugin)
	{
		this.window	= window;
		this.parent	= parent;
		this.plugin	= plugin;
		font	= %[];
	}

	function finalize()
	{
		if(layer != void)
			plugin.releaselayer(layer);
	}

	function setPos(l, t)
	{
		with(this)
		{
			.l	= l;
			.t	= t;
		}
	}

	function setImageSize(w, h)
	{
		with(this)
		{
			.width	= w;
			.height	= h;
		}
	}

	function drawText(l, t, text, color, opa, aa, level, scol, sw, sol, sot, vert)
	{
		with(this)
		{
			.ml		= l;
			.mt		= t;
			.name	= text;
			.col	= color;
			.opacity= opa;
			.aa		= aa;
			.level	= level;
			.scol	= scol;
			.sw		= sw;
			.sol	= sol;
			.sot	= sot;
			.vertical= vert;
		}
	}

	function assignImages(src)
	{
		with(src)
		{
			l		= .l;
			t		= .t;
			width	= .width;
			height	= .height;
			ml		= .ml;
			mt		= .mt;
			name	= .name;
			col		= .col;
			opacity	= .opacity;
			aa		= .aa;
			level	= .level;
			scol	= .scol;
			sw		= .sw;
			sol		= .sol;
			sot		= .sot;
			vertical= .vertical;
			with(.font)
			{
				font.bold		= .bold;
				font.face		= .face;
				font.height		= .height;
				font.italic		= .italic;
				font.angle		= .angle;
			}
		}
	}

	function fillRect() {}
	function setSizeToImageSize() {}

	property left
	{
		setter(l)
		{
			this.l	= l;
			if(parent == kag.fore.base && l<parent.width && l>-width)
			{
				if(layer==void)
				{
					with(layer = plugin.getlayer(window, parent))
					{
						.name	= name;

						//	フォント設定
						.font.bold		= font.bold;
						.font.face		= font.face;
						.font.height	= font.height;
						.font.italic	= font.italic;
						.font.angle		= font.angle;

						//	レイヤーサイズを計算
						var w, h, fw, fh;
						var fws = [], fhs = [];
						for (var i = 0, internal_forloop_count = name.length; i < internal_forloop_count; i += 1)
						{
							fw = .font.getTextWidth(name[i]);
							fh = .font.getTextHeight(name[i]);
							if(plugin.rotatechar.indexOf(name[i]) >= 0)
								fw	<-> fh;
							w	= fw if fw > w;
							h	+= fh;
							fws[i] = fw;
							fhs[i] = fh;
						}
						.setSize(width = w, height = h);
						dm(@"${name} = (${.width}, ${.height})");

						//	文字を描画
						.fillRect(0, 0, .width, .height, 0x00000000);	//	背景透明に。
						var l, t = mt;
						for (var i = 0, internal_forloop_count = name.length; i < internal_forloop_count; i += 1)
						{
							if(plugin.rotatechar.indexOf(name[i]) >= 0)
							{
								.font.angle	= 2700;
								l	= (.width + (fws[i] + 1)) >> 1;
							}
							else
							{
								.font.angle	= 0;
								l	= (.width - (fws[i] + 1)) >> 1;
							}
							var dt;
							if(plugin.minichar.indexOf(name[i]) >= 0)
							{
								l	+= (fws[i] + 1)>>3;
								dt	= -(fhs[i] + 1)>>3;
							}
							.drawText(l, t+dt, name[i], col, opacity, aa, level, scol, sw, sol, sot);
							t	+= fhs[i];
						}

						//	サイズが異なるなら
						if(magx != 1 || magy != 1)
						{
							var	tmp	= plugin.getlayer(window, parent);
							tmp.name= name;

							tmp.setSize(int(w * magx), int(h * magy));
							tmp.stretchCopy(0, 0, tmp.width, tmp.height, layer, 0, 0, w, h, stFastLinear);

							plugin.releaselayer(layer);
							layer	= tmp;
						}
					}
					layer.opacity	= 255;
					layer.visible	= true;
				}
				layer.setPos(l, t);
			}
			else
			{
				plugin.releaselayer(layer);
				layer	= void;
			}
		}
		getter	{ return l; }
	}

	property top
	{
		setter(t)
		{
			this.t	= t;
			if(parent == kag.fore.base && t<parent.height && t>-height)
			{
				//	必要な間だけレイヤーを作成
				if(layer==void)
				{
					with(layer = plugin.getlayer(window, parent))
					{
						.name	= name;

						//	フォント設定
						.font.bold		= font.bold;
						.font.face		= font.face;
						.font.height	= font.height;
						.font.italic	= font.italic;
						.font.angle		= font.angle;
						.font.strikeout	= font.strikeout;
						.font.underline	= font.underline;

						//	レイヤーサイズを計算
						var fw	= .font.getTextWidth(name);
						var fh	= .font.getTextHeight(name);
						.setSize(width = fw + (ml<<1), height = fh + (mt<<1));

						//	文字を描画
						.fillRect(0, 0, .width, .height, 0x00000000);	//	背景透明に。
						if(.font.height < 15)
						{
							var	tmp	= plugin.getTempLayer();
							with(tmp)
							{
								.font.bold		= font.bold;
								.font.face		= font.face;
								.font.height	= font.height * 2;	//	元の2倍で描画して縮小することで、綺麗に表示
								.font.italic	= font.italic;
								.font.angle		= font.angle;
								.font.strikeout	= font.strikeout;
								.font.underline	= font.underline;
								.setSize(.font.getTextWidth(name), .font.getTextHeight(name));
								.fillRect(0, 0, .width, .height, 0);
								.drawText(0, 0, name, col, opacity, aa, level, scol, sw, sol, sot);
							}
							.stretchCopy(ml, mt, fw, fh, tmp, 0, 0, tmp.width, tmp.height, stLinear);
						}
						else
							.drawText(ml, mt, name, col, opacity, aa, level, scol, sw, sol, sot);

						//	サイズが異なるなら
						if(magx != 1 || magy != 1)
						{
							var	tmp	= plugin.getlayer(window, parent);
							tmp.name= name;

							tmp.setSize(int(width * magx), int(height * magy));
							dm(magx, magy, tmp.width, tmp.height, layer.width, layer.height);
							tmp.stretchCopy(0, 0, tmp.width, tmp.height, layer, 0, 0, width, height, stFastLinear);

							plugin.releaselayer(layer);
							layer	= tmp;
						}
					}
					layer.opacity	= 255;
					layer.visible	= true;
				}
				layer.setPos(l, t);
			}
			else
			{
				plugin.releaselayer(layer);
				layer	= void;
			}
		}
		getter	{ return t; }
	}
}

class StaffRollPlugin extends KAGPlugin
{
	var foreBase;
	var backBase;
	var foreLayers = [];
	var backLayers = [];
	var foreStationaryLayers = [];
	var backStationaryLayers = [];
	var currentPos;
	var moveHeight;
	var moveTime;
	var moveStartTick;
	var moving = false;
	var vertical = false;
	var baseline;
	var moveLastTick;
	var trotlevel;
	var stopMoving;
	var bgcolor	= 0xFF000000;
	var	enableKeySkip	= false;

	var	fontFace, fontSize;

	var foreTextLayer;
	var backTextLayer;

	var rotatechar	= "()-（）−ー";
	var minichar	= "ぁぃぅぇぉっゃゅょァィゥェォッャュョ";

	var length;

	function StaffRollPlugin()
	{
		super.KAGPlugin();
	}

	function finalize()
	{
		uninit();
		super.finalize(...);
	}

	function init(elm)
	{
		// スタッフロールの初期化
		uninit();
		trotlevel = elm.trotlevel!==void ? +elm.trotlevel : 10;
		vertical = kag.current.vertical;
		vertical = +elm.vertical if elm.vertical !== void;
		if(!vertical)
			currentPos = 0;
		else
			currentPos = kag.fore.base.width;
		baseline = elm.baseline !== void ? +elm.baseline : 0;
		fontFace = elm.face != void ? elm.face : "ＭＳ Ｐ明朝";

		stopMoving	= false;
		foreBase	= kag.fore.base;
		backBase	= kag.back.base;
		length		= 0;
	}

	var	pools	= [];
	function getlayer(window, parent)
	{
		for (var i = 0, internal_forloop_count = pools.count; i < internal_forloop_count; i += 1)
		{
			if(pools[i].parent == parent)
			{
				var	layer	= pools[i];
				pools.erase(i);
				return layer;
			}
		}
		return new global.Layer(window, parent);
	}

	function releaselayer(layer)
	{
		if(layer != void)
		{
			layer.name		= "in pool";
			layer.visible	= false;
			pools.add(layer);
		}
	}

	var	tmplayer;
	function getTempLayer()
	{
		if(tmplayer == void)
			tmplayer	= new Layer(kag, kag.primaryLayer);
		return tmplayer;
	}

	function addText(opt)
	{
		// スタッフロールにテキストを追加
		//	ロールとは直角の軸の座標が指定されて無い場合は、センタリングされる
		var fore = new TextHolder(kag, kag.fore.base, this);
		var back = new TextHolder(kag, kag.back.base, this);
		fore.absolute = 2000000-5;
		back.absolute = fore.absolute;

		var ff = fore.font;
		var ml = kag.current;

		ml.changeLineSize() if ml.sizeChanged;

		ff.bold		= opt.bold !== void ? +opt.bold : false;
		ff.face		= opt.face !== void ? opt.face : fontFace;
		ff.height	= opt.size !== void ? +opt.size : 18;
		ff.italic	= opt.italic !== void ? +opt.italic : false;

		var	layer	= getlayer(kag, kag.fore.base);
		var	reff	= layer.font;
		reff.bold	= ff.bold;
		reff.face	= ff.face;
		reff.height	= ff.height;
		reff.italic	= ff.italic;

		var cx, cy;
		var text = opt.text;
		cx = reff.getTextWidth(text);
		cy = reff.getTextHeight(text);

		releaselayer(layer);

		var tx, ty;

		var edgewidth	= 4;
		var edgeheight	= ml.edge ? 2 : (ml.shadow ? 4 : 1);
		if(!vertical)
		{
			fore.setImageSize(cx + (edgewidth<<1), cy + (edgeheight<<1));
			tx = edgewidth; ty = edgeheight;
		}
		else
		{
			fore.setImageSize(cy + (edgewidth<<1), cx + (edgeheight<<1));
			tx = cy + (edgewidth<<1) - edgewidth;
			ty = edgeheight;
		}

		fore.magx = back.magx = opt.magx !== void ? +opt.magx : 1;
		fore.magy = back.magy = opt.magy !== void ? +opt.magy : 1;

		fore.face = dfBoth;
		fore.fillRect(0, 0, fore.imageWidth, fore.imageHeight, 0);

		if(ml.edge)
			fore.drawText(tx, ty, text, ml.chColor, 255, ml.antialiased, 512, ml.edgeColor, 1, 0, 0, vertical); // 文字
		else if(ml.shadow)
			fore.drawText(tx, ty, text, ml.chColor, 255, ml.antialiased, 255, ml.shadowColor, 0, 2, 2, vertical); // 文字
		else
			fore.drawText(tx, ty, text, ml.chColor, 255, ml.antialiased,,,,,, vertical); // 文字

		back.setImageSize(fore.imageWidth, fore.imageHeight);
		back.assignImages(fore);

		fore.setSizeToImageSize();
		back.setSizeToImageSize();

		var x, y;
		if(!vertical)
		{
			var	w	= fore.width * fore.magx;
			if(opt.x !== void)
				x	= +opt.x;
			else if(opt.r !== void)
				x	= +opt.r - w;
			else
				x = (kag.primaryLayer.width-w) \ 2 + (opt.cmx!=void ? +opt.cmx : 0);
			x += baseline;
			y = currentPos += +opt.y;
		}
		else
		{
			var	h	= fore.height * fore.magy;
			x = (currentPos -= +opt.x) - cy;
			y = opt.y!==void ? +opt.y : (kag.primaryLayer.height-h) \ 2 + (opt.cmy!=void ? +opt.cmy : 0);
			y += baseline;
		}

		fore.setPos(x, y);
		back.setPos(x, y);
		if(!vertical)
		{
			fore.orgY = y;
			back.orgY = y;
		}
		else
		{
			fore.orgX = x;
			back.orgX = x;
		}

		foreLayers.add(fore);
		backLayers.add(back);
	}

	function addImage(opt)
	{
		// スタッフロールに画像を追加
		//	ロールとは直角の軸の座標が指定されて無い場合は、センタリングされる
		var fore = new Layer(kag, kag.fore.base);
		var back = new Layer(kag, kag.back.base);
		fore.absolute = 2000000-5;
		back.absolute = fore.absolute;

		if(Storages.isExistentImage(opt.storage))
			fore.loadImages(opt.storage);
		else
		{
			dm("addImage: "+opt.storage+" が見つかりませんでした。");
			fore.setSize(320, 320);
			fore.fillRect(0, 0, fore.width, fore.height, 0);
		}
		back.assignImages(fore);

		fore.setSizeToImageSize();
		back.setSizeToImageSize();

		var x, y;
		if(!vertical)
		{
			x = opt.x!==void ? +opt.x : (kag.primaryLayer.width-fore.width) \ 2;
			y = currentPos += +opt.y;
		}
		else
		{
			x = (currentPos -= +opt.x) - fore.imageWidth;
			y = opt.y!==void ? +opt.y : (kag.primaryLayer.height-fore.height) \ 2;
		}

		fore.setPos(x, y);
		back.setPos(x, y);
		if(!vertical)
		{
			fore.orgY = y;
			back.orgY = y;
		}
		else
		{
			fore.orgX = x;
			back.orgX = x;
		}

		foreLayers.add(fore);
		backLayers.add(back);
	}

	function addTransitionImage(opt)
	{
		//	定点に出現して消える画像
		var fore = new Layer(kag, kag.fore.base);
		var back = new Layer(kag, kag.back.base);
		fore.absolute = 2000000-10;
		back.absolute = fore.absolute;

		//	設定
		var	h	= kag.fore.base.height;
		with(fore)
		{
			if(Storages.isExistentImage(opt.storage))
				.loadImages(opt.storage);
			else
			{
				dm("addTransitionImage: "+opt.storage+" が見つかりませんでした。");
				.setImageSize(320, 320);
				.fillRect(0, 0, .imageWidth, .imageHeight, 0);
			}
			.setSizeToImageSize();
			.setPos(+opt.x, +opt.y);
			.opacity	= opt.opac!==void ? +opt.opac : 0;
			.lopacity	= opt.lopac!==void ? +opt.lopac : 255;
			.appearposition		= currentPos + +opt.apos - h;	//	表示開始位置
			.disappearposition	= currentPos + +opt.dpos - h;	//	消去開始位置
			.appeartime			= opt.atime!==void ? +opt.atime : 800;	//	表示フェード時間
			.disappeartime		= opt.dtime!==void ? +opt.dtime : 800;	//	消去フェード時間
			.status		= 0;	//	表示されてない状態
			.x		= .left;	//	初期位置
			.y		= .top;
			.lx		= opt.lx!==void ? +opt.lx : +opt.x;		//	終了時位置
			.ly		= opt.ly!==void ? +opt.ly : +opt.y;
			.len	= .disappearposition - .appearposition;	//	全ての処理を行う区間の長さ
//			.type	= ltAdditive;
//			.visible= false;
		}
		with(back)
		{
			.assignImages(fore);
			.setSizeToImageSize();
			.setPos(+opt.x, +opt.y);
			.opacity	= 0;
//			.type	= ltAdditive;
//			.visible= false;
		}

		foreStationaryLayers.add(fore);
		backStationaryLayers.add(back);
	}

	function startMove(height, time)
	{
		dm("start move("+height+", "+time+")");
		// 移動を開始

		if(moving) return;

		moveLastTick = moveStartTick = System.getTickCount();
		moveHeight = height;
		moveTime = time;
		var spd = height / time;

		for (var i = 0, internal_forloop_count = foreLayers.count; i < internal_forloop_count; i += 1)
		{
			foreLayers[i].visible = true;
			backLayers[i].visible = true;
		}
		for (var i = 0, internal_forloop_count = foreStationaryLayers.count; i < internal_forloop_count; i += 1)
		{
			with(foreStationaryLayers[i])
				.len	+= int(.disappeartime * spd);	//	レイヤーが表示している間に移動する距離
		}

		moving = true;
		System.addContinuousHandler(moveHandler);
	}

	function stopMove()
	{
		if(moving)
		{
			kag.trigger("staffroll");
			moving = false;
			System.removeContinuousHandler(moveHandler);
		}
	}

	function uninit()
	{
		// 停止とクリーンアップ
		stopMove();

		for (var i = 0, internal_forloop_count = foreLayers.count; i < internal_forloop_count; i += 1)
			invalidate foreLayers[i];
		for (var i = 0, internal_forloop_count = backLayers.count; i < internal_forloop_count; i += 1)
			invalidate backLayers[i];
		for (var i = 0, internal_forloop_count = foreStationaryLayers.count; i < internal_forloop_count; i += 1)
			invalidate foreStationaryLayers[i];
		for (var i = 0, internal_forloop_count = backStationaryLayers.count; i < internal_forloop_count; i += 1)
			invalidate backStationaryLayers[i];
		for (var i = 0, internal_forloop_count = pools.count; i < internal_forloop_count; i += 1)
			invalidate pools[i] if pools[i] != void;

		foreLayers.count = 0;
		backLayers.count = 0;
		foreStationaryLayers.count	= 0;
		backStationaryLayers.count	= 0;
		pools.clear();
		if(tmplayer != void)
		{
			invalidate tmplayer;
			tmplayer	= void;
		}
	}

	function uninitPage(page="back")
	{
		var ls, sls;
		if(kag[page].base == foreBase)
		{
			ls	= foreLayers;
			sls	= foreStationaryLayers;
		}
		else
		{
			ls	= backLayers;
			sls	= backStationaryLayers;
		}

		for (var i = 0, internal_forloop_count = ls.count; i < internal_forloop_count; i += 1)
			invalidate ls[i];
		for (var i = 0, internal_forloop_count = sls.count; i < internal_forloop_count; i += 1)
			invalidate sls[i];
		ls.count	= 0;
		sls.count	= 0;
	}

	var scrollPos;
	function moveHandler(tick)
	{
		if(kag==void || !(kag isvalid))
			return;
		if(stopMoving)
		{
			stopMove();
			return;
		}
		if(tick <= moveLastTick)
			return;

		// 移動ハンドラ
		var time	= tick - moveStartTick;
		if(time>moveTime)
		{
			time	= moveTime;
			stopMoving	= true;	//	次の呼び出しで停止
		}
		if(enableKeySkip && (System.getKeyState(VK_CONTROL) || System.getKeyState(VK_PAD1)))
			moveStartTick	-= (tick - moveLastTick) * (trotlevel - 1);	// 何倍速？
		moveLastTick	= tick;
		var current	= scrollPos = moveHeight * time \ moveTime;

		var laycount;

//		foreTextLayer.parent	= kag.fore.base;

		//	文字と固定画像
		laycount = foreLayers.count;
		var f = foreLayers;
		var b = backLayers;
		if(!vertical)
		{
			for (var i = 0, internal_forloop_count = laycount; i < internal_forloop_count; i += 1)
			{
				var fl = f[i], bl = b[i];
				fl.top = bl.top = fl.orgY - current;
			}
		}
		else
		{
			for (var i = 0, internal_forloop_count = laycount; i < internal_forloop_count; i += 1)
			{
				var fl = f[i], bl = b[i];
				fl.left = bl.left = fl.orgX + current;
			}
		}

		//	現われたり消えたりする画像
		var	fs		= foreStationaryLayers;
		var	bs		= backStationaryLayers;
		laycount	= fs.count;
		for(var i=laycount-1; i>=0; i--)
		{
			with(fs[i])
			{
				if(.status>3)	continue;	//	終了したら用は無い
				switch(.status)
				{
				case 0:	//	非表示
					if(current>=.appearposition)
					{
						.status		= 1;		//	表示開始
						.appearstart	= time;
						.visible = bs[i].visible	= true;
					}
					break;

				case 1:	//	表示中
					if(time>=.appearstart+.appeartime)
						.status	= 2;	//	静止
					else
					{
						//	徐々に
						.opacity	= int((time - .appearstart) / .appeartime * .lopacity);
						bs[i].opacity	= .opacity;
					}
					break;

				case 2:	//	静止
					if(current>=.disappearposition)
					{
						.status		= 3;	//	消去開始
						.disappearstart= time;
					}
					break;

				case 3:	//	消去中
					if(time>=.disappearstart+.disappeartime)
					{
						.status	= 4;
						.visible = bs[i].visible	= false;
					}
					else
					{
						.opacity	= int((.disappeartime - (time - .disappearstart)) /
							.disappeartime * .lopacity);
						bs[i].opacity	= .opacity;
					}
					break;
				}
				//	レイヤー移動
				if(.status>0)
				{
					var per = (current - .appearposition) / .len;
					.setPos(int((.lx - .x) * per) + .x, int((.ly - .y) * per) + .y);
					bs[i].setPos(.left, .top);
				}
			}
		}
	}
}

kag.addPlugin(global.staffroll_object = new StaffRollPlugin());
	// プラグインオブジェクトを作成し、登録する

//-----------------------------------------------------------------------------
//	マクロ定義
var	kt	= kag.tagHandlers;

kt.staffrollinit	= function(elm)
{
	staffroll_object.init(elm);
	return 0;
} incontextof kag;

kt.staffrollsetting	= function(elm)
{
	staffroll_object.bgcolor	= elm.bgcolor;
	return 0;
} incontextof kag;

kt.staffrolltext	= function(elm)
{
	staffroll_object.addText(elm);
	return 0;
} incontextof kag;

kt.staffrollimage	= function(elm)
{
	staffroll_object.addImage(elm);
	return 0;
} incontextof kag;

//-----------------------------------------------------------------------------
//	スクロール開始
//		width, height	: スクロール総延長("+数値"とすることで、それまでに追加した長さ＋αとすることができる)
//		time			: スクロール時間(speedと排他)
//		speed			: 1秒当りのスクロール量(timeと排他)
//-----------------------------------------------------------------------------
kt.staffrollstart	= function(elm)
{
	dmt("staffrollstart");
	var	h	= elm.width !== void ? elm.width : elm.height;
	h	= h[0] == "+" ? (Math.abs(staffroll_object.currentPos) + int(h.substr(1))) : (int h);
	var	tm	= elm.speed !== void ? (h / +elm.speed * 1000) : +elm.time;
	staffroll_object.startMove(h, tm);
	tagHandlers.resetwait(%[]);
	return 0;
} incontextof kag;

kt.staffrolluninit	= function(elm)
{
	staffroll_object.uninit(elm);
	return 0;
} incontextof kag;

//-----------------------------------------------------------------------------
//	バックで画像をトランジションさせるためのサポート機能
//	mode=untilなwait(time=原点からの経過時間/past=直前のuwaitからの経過時間)
kt.uwait	= function(elm)
{
	conductor.insert(0, %[tagname:"wait", mode:"until", time:(elm.time!==void ? elm.time : .lastUWaitTime+elm.past)]);
	conductor.insert(1, %[tagname:"eval", exp:".lastUWaitTime=System.getTickCount()"]);
	return 0;
} incontextof kag;

//-----------------------------------------------------------------------------
//	指定ラインが表示されたら画像を表示するプラグイン
//		apos : 表示開始する高さ/0ならコマンドを置いた位置で表示開始
//		dpos : 表示開始する高さ/0なら表示が完了したあとすぐに消去開始
//		atime: 表示フェード時間, dtime: 消去フェード時間
//		storage, x, y はいつもどおり
//-----------------------------------------------------------------------------
kt.staffrollappearimage	= function(elm)
{
	staffroll_object.addTransitionImage(elm);
	return 0;
} incontextof kag;

//	スタッフロールの進行が終了するまで待つ
kt.wstaffroll	= function(elm)
{
	if(staffroll_object.moving)
	{
		elm.name	= "staffroll";
		elm.onskip	= "staffroll_object.uninit()";
		elm.canskip	= false;
		return tagHandlers.waittrig(elm);
	}
	else
		return 0;
} incontextof kag;

//	ページを指定してスタッフロールオブジェクトを削除
kt.erasestaffroll	= function(elm)
{
	staffroll_object.uninitPage(elm.page);
	return 0;
} incontextof kag;

delete kt;
@endif
